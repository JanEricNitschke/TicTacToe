Replace minus with ~
and     plus  with *
so that this text is a valid brainfuck comment

I need to store the board and the currently active player

Also need the board layout
Will probably use the C/assembly one
 ? | ? | ? \n (Newline is 10 in asci)
~~~*~~~*~~~\n
 ? | ? | ? \n
~~~*~~~*~~~\n
 ? | ? | ? \n
~~~*~~~*~~~

Using asci values
With the 0 being replaced by 0~8 * 48
(32)0(32)(124)(32)0(32)(124)(32)0(32)(10)
(45)(45)(45)(43)(45)(45)(45)(43)(45)(45)(45)(10)

Player is: 88 (X) or 79 (O)
So maybe lets do:
    Player  ~~~~~~~~~~~~~Board~~~~~~~~~~~~~~~~ ~~~~~~~~~~~~~Template board rows~~~~~~~~~~~~~  ~~~~~~~Template board fillers~~~~~~~~~~~~~~~~~~  ~~~~~~~~~~~~~~~~~~~~~Scratch space for logic ~~~~~~~~~~~~~~
(0)(Player)(B1)(B2)(B3)(B4)(B5)(B6)(B7)(B8)(B9)(32)(0)(32)(124)(32)(0)(32)(124)(32)(0)(32)(10)(45)(45)(45)(43)(45)(45)(45)(43)(45)(45)(45)(10)()()()()()()()()()()()()()()()()()()()()()()()()()()()()()()
BoardPositions: 3;4;5;6;7;8;9;10;11
Placeholder Positions: 13;17;21
Scratch space starts at: 36
Game ongoing flag at 56
We leave the first spot empty to be able to get back there easily

Scratch space needs to be used to check for valid input 0~8 empty ~ board
To perform if checks for draw and win

Starting at 60: Store messages:
Where to make your next move? [0~8]\n    Message1
Player turn: (Player\n)                  Message2
Player wins the game: (Player\n)         Message3
Match drawn!\n                           Message4
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
They will be null separated:
Thus; we can print them easily:
Also easily allows going to the nth message:

So lets write the code that prepares this and then prints the board
>                                           Go left once to leave the first spot open
     11     x    8 = 88 = X
Player
>+++++++++++[<++++++++>-]                    X
Board
>++++++[<++++++++>-]                         0
>++++++[<++++++++>-]<+>                      1
>++++++[<++++++++>-]<++>                     2
>++++++[<++++++++>-]<+++>                    3
>++++++[<++++++++>-]<++++>                   4
>++++++[<++++++++>-]<+++++>                  5
>++++++[<++++++++>-]<++++++>                 6
>++++++[<++++++++>-]<+++++++>                7
>++++++[<++++++++>-]<++++++++>               8
Initialize the template1 entries with 0;1;2
>++++[<++++++++>-]                           ' '
>++++++[<++++++++>-]                         0
>++++[<++++++++>-]                           ' '
>++++[<+++++++++++++++++++++++++++++++>-]    |
>++++[<++++++++>-]                           ' '
>++++++[<++++++++>-]<+>                      1
>++++[<++++++++>-]                           '
>++++[<+++++++++++++++++++++++++++++++>-]    |
>++++[<++++++++>-]                           ' '
>++++++[<++++++++>-]<++>                     2
>++++[<++++++++>-]                           ' '
>++[<+++++>-]                                \n
Template2
>+++++[<+++++++++>-]                         Minus (45)
>+++++[<+++++++++>-]                         Minus
>+++++[<+++++++++>-]                         Minus
>+++++[<+++++++++>-]<-->                     Plus  (43)
>+++++[<+++++++++>-]                         Minus (45)
>+++++[<+++++++++>-]                         Minus
>+++++[<+++++++++>-]                         Minus
>+++++[<+++++++++>-]<-->                     Plus  (43)
>+++++[<+++++++++>-]                         Minus (45)
>+++++[<+++++++++>-]                         Minus
>+++++[<+++++++++>-]                         Minus
>++[<+++++>-]                                \n

We end on the first scratch space (36)
Write the messages
>>>>>>>>>>>>>>>>>>>>>>>> (60)
Where to make your next move? [0~8]\n
>+++++++++++[<++++++++>-]<->         W
>++++++++[<+++++++++++++>-]          h
>++++++++[<+++++++++++++>-]<--->     e
+++[>++++++<-]>+[<++++++>-]          r
>++++++++[<+++++++++++++>-]<--->     e
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<++>      t
+++[>++++++<-]>+[<++++++>-]<--->     o
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<----->   m
>++++++++[<++++++++++++>-]<+>        a
>++++++++[<+++++++++++++>-]<+++>     k
>++++++++[<+++++++++++++>-]<--->     e
>++++[<++++++++>-]                   ' '
>+++++++++++[<+++++++++++>-]         y
+++[>++++++<-]>+[<++++++>-]<--->     o
+++[>++++++<-]>+[<++++++>-]<+++>     u
+++[>++++++<-]>+[<++++++>-]          r
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<---->    n
>++++++++[<+++++++++++++>-]<--->     e
>+++++++++++[<+++++++++++>-]<->      x
+++[>++++++<-]>+[<++++++>-]<++>      t
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<----->   m
+++[>++++++<-]>+[<++++++>-]<--->     o
>+++++++++++[<+++++++++++>-]<--->    v
>++++++++[<+++++++++++++>-]<--->     e
>+++++++[<+++++++++>-]               ?
>++++[<++++++++>-]                   ' '
>+++++++[<+++++++++++++>-]           Square braces open
>++++++[<++++++++>-]                 0
>+++++[<+++++++++>-]                 Minus
>++++++[<++++++++>-]<++++++++>       8
>+++++++[<+++++++++++++>-]<++>       Square braces closed
>++[<+++++>-]                        \n
>                                    One empty space
Player turn: (Player\n)
>++++++++[<++++++++++>-]             P
>+++++++++[<++++++++++++>-]          l
>++++++++[<++++++++++++>-]<+>        a
>+++++++++++[<+++++++++++>-]         y
>++++++++[<+++++++++++++>-]<--->     e
+++[>++++++<-]>+[<++++++>-]          r
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<++>      t
+++[>++++++<-]>+[<++++++>-]<+++>     u
+++[>++++++<-]>+[<++++++>-]          r
+++[>++++++<-]>+[<++++++>-]<---->    n
>+++++++[<++++++++>-]<++>            :
>++++[<++++++++>-]                   ' '
>
Player wins the game: (Player\n)
>++++++++[<++++++++++>-]             P
>+++++++++[<++++++++++++>-]          l
>++++++++[<++++++++++++>-]<+>        a
>+++++++++++[<+++++++++++>-]         y
>++++++++[<+++++++++++++>-]<--->     e
+++[>++++++<-]>+[<++++++>-]          r
>++++[<++++++++>-]                   ' '
>+++++++++++[<+++++++++++>-]<-->     w
>++++++++[<+++++++++++++>-]<+>       i
+++[>++++++<-]>+[<++++++>-]<---->    n
+++[>++++++<-]>+[<++++++>-]<+>       s
>++++[<++++++++>-]                   ' '
+++[>++++++<-]>+[<++++++>-]<++>      t
>++++++++[<+++++++++++++>-]          h
>++++++++[<+++++++++++++>-]<--->     e
>++++[<++++++++>-]                   ' '
>++++++++[<+++++++++++++>-]<->       g
>++++++++[<++++++++++++>-]<+>        a
+++[>++++++<-]>+[<++++++>-]<----->   m
>++++++++[<+++++++++++++>-]<--->     e
>+++++++[<++++++++>-]<++>            :
>++++[<++++++++>-]                   ' '
>
Match drawn!\n
>+++++++[<+++++++++++>-]             M
>++++++++[<++++++++++++>-]<+>        a
+++[>++++++<-]>+[<++++++>-]<++>      t
>++++++++[<++++++++++++>-]<+++>      c
>++++++++[<+++++++++++++>-]          h
>++++[<++++++++>-]                   ' '
>++++++++[<++++++++++++>-]<++++>     d
+++[>++++++<-]>+[<++++++>-]          r
>++++++++[<++++++++++++>-]<+>        a
>+++++++++++[<+++++++++++>-]<-->     w
+++[>++++++<-]>+[<++++++>-]<---->    n
>++++[<++++++++>-]<+>                !
>++[<+++++>-]                        \n
Currently after message 4
<[<] Back to after message 3
<[<] Back to after message 2
<[<] Back to after message 1
<[<] Back to before start of message1 (59)
<<<<<<<<<<<<<<<<<<<<<<<

Gameloop flag is 20 from scratch spot
>>>>>>>>>>>>>>>>>>>>+
[
<<<<<<<<<<<<<<<<<<<<
Player turn: (Player\n)
>>>>>>>>>>>>>>>>>>>>>>>> (At 60; start of message1; need Message2)
[>]> (At the first spot of message2)
[.>] (Print the message)
<[<] Before message2
<[<] Before message1
<<<<<<<<<<<<<<<<<<<<<<<< (At the start of scratch) (36)

<[<] Go left until we hit a 0 (start)
>. (Show the player)
>>>>>>>>>>>>>>>>>>>>>. (Show a newline; there is one at 23
<[<] Go back to the start
Logic for show_board
Fill in the placeholders:
Row1
Column1
temp0=36
x=13
y=3
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>  Go to temp0 from the start
[-]  Empty scratch space
<<<<<<<<<<<<<<<<<<<<<<<[-]  Go to x=placeholder and empty
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-] Go to board spot and move to x and temp0
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-] Move from temp0 back to board

Column2
temp0=36
x=17
y=4
[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<[>>>>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

Column3
temp0=36
x=21
y=5
[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<<<<[>>>>>>>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

Go to start of template (12) from temp0 (36)
<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.> (back to temp0 while printing everything)

Row2
[-]
<<<<<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<[>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<[>>>>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.> (back to temp0 while printing everything)

Row3
[-]
<<<<<<<<<<<<<<<<<<<<<<<[-]
<<<<[>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>-]

[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<[>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>-]

[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>-]

<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>>>>>>>>>>>>> (back to temp0 while printing everything)

Where to make your next move? [0~8]\n
>>>>>>>>>>>>>>>>>>>>>>>> (At 60; start of message1; need Message1)
[.>] (Print the message)
<[<] Back to before start of message1 (59)
<<<<<<<<<<<<<<<<<<<<<<<

Into temp0 read a character that matches 0~8
And is unoccupied
Layout is:
(input)(scratch1)(scratch2)(scratch3)
Set the valid input flag to 1 to enter the loop
>>>>>>+[<<<<<<
  +[ Enter the loop
    ,  Grab user input
    Subtract 48 from the input to get the actual int
    ------------------------------------------------
    >[-]>[-]>[-]<<<  Make sure that the scratch space is empty
    [>>+>+<<<-]>>>[<<<+>>>-]<<<  Copy the input into scratch number 2
    Check the input vs the allowed character
    [[>+<-]]>[<+>-]<                      0
    (-)[(+)[>+<-]]>[<+>-]<                1
    (--)[(++)[>+<-]]>[<+>-]<              2
    (---)[(+++)[>+<-]]>[<+>-]<            3
    (----)[(++++)[>+<-]]>[<+>-]<          4
    (-----)[(+++++)[>+<-]]>[<+>-]<        5
    (------)[(++++++)[>+<-]]>[<+>-]<      6
    (-------)[(+++++++)[>+<-]]>[<+>-]<    7
    (--------)[(++++++++)[>+<-]]>[<+>-]<  8
  ]
  >>[<<+>>-]<<  Move the valid input back into temp0


  If input /= 0
  >[-]+
  >[-]
  <<[>-]> Code to execute if input /= 0 : None
  [<
    Code to execute if input == 0
    >>>[-]  Move three to the right to get a free scratch space (Spot 39)
    First check if the value at spot 0 is actually 0
    For that: Copy the value into scratch
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<  Go to board spot 0
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    Subtract 48
    ------------------------------------------------
    Start the unoccupied check
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]        Go to board spot 0 (3)
      Go to the currently active player
      Copy to scratch and board
      <[>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      Move from scratch to active player
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    Clear the flag to restart the loop
    >-<]<<  End of unoccupied check
    [-]
    <<<
  >->]<<  End of the input was 0 check
  Now check for input == 1
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    -------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<[>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 2
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    --------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<[>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 3
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    ---------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<[>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 4
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    ----------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<<[>>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 5
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    -----------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<<<[>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 6
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    ------------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<<<<[>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 7
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    -------------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<<<<<[>>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
  Now check for input == 8
  -
  >[-]+
  >[-]
  <<[>-]>
  [<
    >>>[-]
    >[-]
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    [>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
    >>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
    --------------------------------------------------------
    >[-]+
    >[-]
    <<[>-]>
    [<
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<[-]
      <<<<<<<<<[>>>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
      >->
    >-<]<<
    [-]
    <<<
  >->]<<
>>>>>>]
<<<<<<[+]  Reset the scratch space to 0



~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Logic for game win
Memory layout will be
(Counter; Player; Player; BoardSpot;)
Copy the current player closer
<[<]>
[>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<<<



>>>>>>>>>>>>>>>>>>>>
Only check if the game is still ongoing
Rows
0;1;2
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<
  <<<<[<]>>  Board spot 0
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  Subtract Player from Boardspot and go back to boardspot
  <<[->-<]>
  Check if boardspot is zero now
  >[-]+
  >[-]
  <<[>-]>  Boardspot is not zero
  [<  Boardspot is zero
  Go to counter and inc
  <<<+>>>
  >->]<<
  Spot 1
  [-]        Empty the Boardspot
  <[-]       Empty the subtracted copy of the player
  <[>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  Subtract Player from Boardspot and go back to boardspot
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 2
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  Subtract Player from Boardspot and go back to boardspot
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Check if we had three hits
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[   We have not had three hits
          So the game keeps going
  [-]     Clear the counter
  >>>[-]  Clear the board value
  <<<     Back to the counter
  >>-]>>
  [<<
  Spot is zero: So the game is over because the current player has won:
  Clear and set the ongoing flag to zero
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<



3;4;5
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>>>>  Board spot 3
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 4
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 5
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

6;7;8
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>  Board spot 6
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 7
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 8
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

Cols

0;3;6
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>  Board spot 0
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 3
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 6
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

1;4;7
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>>  Board spot 1
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 4
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 7
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

2;5;8
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>>>  Board spot 2
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 5
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 8
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

Diagonals

0;4;8
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>  Board spot 0
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 4
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 8
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<

2;4;6
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<
  [>+>+<<-] Copy the current player
  >>[<<+>>-]
  <<<<[<]>>>>  Board spot 2
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 4
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  Spot 6
  [-]
  <[-]
  <[>+>+<<-]
  >>[<<+>>-]
  <<<<[<]>>>>>>>>
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
  <<[->-<]>
  >[-]+
  >[-]
  <<[>-]>
  [<
  <<<+>>>
  >->]<<
  <<<
  ---
  >>[-]+
  >>[-]
  <<<<[
  [-]
  >>>[-]
  <<<
  >>-]>>
  [<<
  [-]>[-]>>[-]
  >>>>>>>>>>>>>>>>>[-]
  <<<<<<<<<<<<<<<<<<<<
  >>->>]<<<<
>>>>>>>>>>>>>>>>>>>>>-]>
[<>->]<<


~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

>[-]+
>[-]
<<[
  Check for draw
  Set game ongoing flag to 0
  Will set back to 1 if any field is empty
  [-]<<<<<<<<<<<<<<<<<<<<
  Layout will be (value if empty)(copy of value if empty)(actual value)
  Will be starting at spot 36 and moving one to the right for each check
  This means that for spot 8 the positions are 45;46;47
  Use 49 as marker that we are done
  >>>>>>>>>>>>>[-]+<<<<<<<<<<<<<
  Need to be at spot (value if empty) plus 4 to check if done
  +
  [
    -
    Done all 8 check spot (up to 49)
    >>>>
    >+
    >
    <<[
      We have done all 8 and all were occupied (otherwise we would have broken out already
      Thus the game is a draw and we need to not set the flag: We are just done
      and only need to do cleanup
      -  Decrement the current value (49)

      Match drawn!\n                           Message4
      >>>>>>>>>>> (At 60; start of message1; need Message4)
      [>]> (At the first spot of message2)
      [>]> (At the first spot of message3)
      [>]> (At the first spot of message4)
      [.>] (Print the message)
      <[<] Before message4
      <[<] Before message3
      <[<] Before message2
      <[<] Before message1 (59)
      <<<<<<<<<< (Back to 49)

      <<<<[-]  Zero the unused value prepared by the previous iteration
    >>>>>-]>
    [<
      <<<<  Go back to first scratch value
      Copy it one to the right
      >>[-]
      <[-]
      <[>+>+<<-]
      >>[<<+>>-]
      Copy the current board position the third spot
      Current board position is always 38~3 to the left of the first scratch spot
      We are now already at the third spot
      >[-]
      <[-]
      <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<[>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
      >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]<
      Decrement by 48
      ------------------------------------------------
      Decrement by (copy of value if empty)
      <[->-<]>
      Check if it is now zero:
      >+
      >
      <<[   If not so: we keep going
        Increment the expected value and move it one to the right
        We increment by two because we subtract one at the start of the loop again
        <<++[>+<-]  This ends us on the old "value if empty"
        Zero the value to the right of the new one
        >>[-]
        >-  Zero the condition flag
        >>-  Zero the out condition flag
        <<<  Get in a proper position to deal with the rest of the if control flow
      ]>
      [<  Otherwise: we are done because it is open and the game keeps going
        So clean up and reset the game ongoing flag to 1
        <<[-]>[-]>>->>-  Set the (value if empty)(copy of value if empty) and condition flags to 0
        Find and reduce the value at 49 by 1
        Can be at most at value ~50; so go 15 to the left
        <<<<<<<<<<<<<<<[>] >>>>>>>>>>>>>[-]
        Find and set the game ongoing flag
        <<<<<<<<<<<<< >>>>>>>>>>>>>>>>>>>>[-]+<
        Rest can do the automatic four moves as they are all zero and then the loop will end
      ]<<
    >>]<<
  ]
  We can either be at the game ongoing flag minus 3 (53) or (49)
  <<<<<<<<<<<<<<<<<<<<[>]  Go back to the first scratch value
>>>>>>>>>>>>>>>>>>>>>-]>
Game is not ongoing anymore: As such the player has won the game
[<
Player wins the game: (Player\n)         Message3
We start off at (56)
>>>> (At 60; start of message1; need Message3)
[>]> (At the first spot of message2)
[>]> (At the first spot of message3)
[.>] (Print the message)
<[<] Before message3
<[<] Before message2
<[<] Before message1
<<<<<<<<<<<<<<<<<<<<<<<< (At the start of scratch) (36)
<[<] Go left until we hit a 0 (start)
>. (Show the player)
>>>>>>>>>>>>>>>>>>>>>. (Show a newline; there is one at 23)
[>] Start of scratch (36)
>>>>>>>>>>>>>>>>>>>> (56)
>->]<<



Swap player (only if the game is ongoing):
>[-]+
>[-]
<<[
  <<<<<<<<<<<<<<<<<<<<
  <[<]>  Go to player
  Move current player into scratch
  [>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
  Subtract 'O' (79)
  -------------------------------------------------------------------------------
  >[-]+
  >[-]
  <<[
    If not zero: It was 'X':
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<  Go back to active player
    Add back 79 to get 'O'
    +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]>
  [<
    It was zero: It was 'O':
    <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>->]<<
  [-]  Clear scratch
  Finish check for win
  Add messages for player turn; next move; draw; win
  >>>>>>>>>>>>>>>>>>>>
>-]>
[<>->]<<
]


<<<<<<<<<<<<<<<<<<<<
<[<]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
[-]
<<<<<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<[>>>>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<<<<[>>>>>>>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>
[-]
<<<<<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<[>>>>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<<<<[>>>>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>.>
[-]
<<<<<<<<<<<<<<<<<<<<<<<[-]
<<<<[>>>>+>>>>>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<<<<<[-]
<<<<<<<[>>>>>>>+>>>>>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>>-]
[-]
<<<<<<<<<<<<<<<[-]
<<<<<<<<<<[>>>>>>>>>>+>>>>>>>>>>>>>>>+<<<<<<<<<<<<<<<<<<<<<<<<<-]
>>>>>>>>>>>>>>>>>>>>>>>>>[<<<<<<<<<<<<<<<<<<<<<<<<<+>>>>>>>>>>>>>>>>>>>>>>>>>-]
<<<<<<<<<<<<<<<<<<<<<<<<
.>.>.>.>.>.>.>.>.>.>.>.>>>>>>>>>>>>>
