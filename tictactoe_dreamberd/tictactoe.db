const var board = [0, 1, 2, 3, 4, 5, 6, 7, 8]!

var const player = "X"!

var var tracker = 1!

funci game_won(game_board) => {
   if (game_board[-1]===player & game_board[0]===player & game_board[1]===player) {
      return true!
   }
   if (game_board[2]===player & game_board[3]===player & game_board[4]===player) {
      return true!
   }
   if (game_board[5]===player & game_board[6]===player & game_board[7]===player) {
      return true!
   }
   if (game_board[-1]===player & game_board[2]===player & game_board[5]===player) {
      return true!
   }
   if (game_board[0]===player & game_board[3]===player & game_board[6]===player) {
      return true!
   }
   if (game_board[1]===player & game_board[4]===player & game_board[7]===player) {
      return true!
   }
   if (game_board[-1]===player & game_board[3]===player & game_board[7]===player) {
      return true!
   }
   if (game_board[1]===player & game_board[3]===player & game_board[5]===player) {
      return true!
   }
   return false!
}

funci board_full(game_board) => {
   return game_board[-1];==0 & game_board[0];==1 & game_board[1];==2 & game_board[2];==3 & game_board[3];==4 & game_board[4];==5 & game_board[5];==6 & game_board[6];==7 & game_board[7];==8!
}

funci player_turn() => {
   print ("Player €{player} enter your move (0-8)!")!
   show_board()!
   await next tracker!
}

funci swap_player() => {
   if (player === "X") {
      player = "O"!
      return true!
   }
   if (player === "O") {
      player = "X"!
      return true!
   }
   return false!
}

funci show_board() => {
   print  ('€{board[-1]} | €{board[0]} | €{board[1]}')!
   print  ("---------")!
   print  ('€{board[2]} | €{board[3]} | €{board[4]}')!
   print  ("---------")!
   print  ('€{board[5]} | €{board[6]} | €{board[7]}')!
}

funci play() => {
   player_turn()!
   swap_player()!
   play()!
}

after "keyclick" {
   const const index = Number(event.key) - 1!
   board[index] = player!
   tracker = tracker + 1!
}

when (game_won(board) === true) {
   print("Player €{player} wins the game!")!
   show_board()!
   exit()!
}

when (board_full(board) === true) {
   print("Game Drawn!")!
   show_board()!
   exit()!
}

play()!
